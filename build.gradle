/*
 * Copyright © 2011 - 2013 Aaron Mahan.
 * Copyright © 2013 - 2016 Forerunner Games, LLC.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 */

import groovy.json.JsonSlurper

buildscript {
  ant.get (src: "http://libgdx.badlogicgames.com/libgdx-site/service/getVersions?release=false", dest: "versions.json", ignoreerrors: "true", verbose: "on")
  def versionFile = file ("versions.json")
  def json
  if (versionFile.exists ()) {
    json = new JsonSlurper ().parseText (versionFile.text)
  } else throw new GradleException ("Unable to retrieve latest LibGDX versions, please check your internet connection.")

  ext {
    gdxVersion = json.libgdxSnapshot
    roboVMVersion = json.robovmVersion
    roboVMGradleVersion = json.robovmPluginVersion
    androidToolsVersion = json.androidBuildtoolsVersion
    androidSDKVersion = json.androidSDKVersion
    androidGradleToolsVersion = json.androidGradleToolVersion
    logbackClassicVersion = "1.1.3"
    jcommanderVersion = "1.48"
    fgToolsVersion = "1.0-SNAPSHOT"
  }

  repositories {
    jcenter ()
    mavenCentral ()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
  }

  dependencies {
    classpath "com.android.tools.build:gradle:$androidGradleToolsVersion"
    classpath "org.robovm:robovm-gradle-plugin:$roboVMGradleVersion"
  }
}

plugins {
  id "com.github.ben-manes.versions" version "0.11.3"
  id "net.saliman.cobertura" version "2.3.0"
}

apply plugin: "cobertura"

task wrapper (type: Wrapper) {
  gradleVersion = "2.10"
}

// Apply to parent project & all subprojects
allprojects {
  apply plugin: "eclipse"
  apply plugin: "idea"

  group = projectGroup
  version = projectVersion

  ext {
    appName = projectName
  }

  repositories {
    mavenLocal ()
    jcenter ()
    mavenCentral ()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.jfrog.org/oss-snapshot-local/" }
    maven { url  "http://dl.bintray.com/forerunnergames/repository" }
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, "seconds"
  }
}

// Apply to all subprojects except android.
configure (subprojects - project (":android")) {
  apply plugin: "java"
  apply plugin: "cobertura"

  sourceCompatibility = projectSourceCompatibility
  targetCompatibility = projectTargetCompatibility

  dependencies {
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.mockito:mockito-core:1.9.5"
    testRuntime "ch.qos.logback:logback-classic:$logbackClassicVersion"
  }
}

// Apply to parent project & all subprojects except android & integration.
//
// We cannot configure the android module here because the cobertura plugin must be applied after
// the android plugin, which has not happened yet in this block. See the android project configuration
// in the root build.gradle for that specific cobertura configuration.
//
// We cannot configure the integration module here because the cobertura task must run only after
// integration::integrationTest, not integration::test. Note: cobertura integration test coverage is broken.
configure (allprojects - [project (":android"), project (":integration")]) {
  test.finalizedBy (project.tasks.cobertura)
}

project (":desktop") {
  dependencies {
    compile project (":client")
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    compile "com.beust:jcommander:$jcommanderVersion"
    runtime "ch.qos.logback:logback-classic:$logbackClassicVersion"
  }
}

project (":android") {
  apply plugin: "android"
  apply plugin: "cobertura"

  configurations { natives }

  dependencies {
    compile (project (":client")) {
      exclude module: "aws-java-sdk"
    }
    compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
    compile "org.slf4j:slf4j-android:1.7.13"
    compile "com.amazonaws:aws-android-sdk-s3:2.2.9"
  }

  test.finalizedBy (project.tasks.cobertura)
}

project (":ios") {
  apply plugin: "robovm"

  dependencies {
    compile project (":client")
    compile "org.robovm:robovm-rt:${roboVMVersion}"
    compile "org.robovm:robovm-cocoatouch:${roboVMVersion}"
    compile "com.badlogicgames.gdx:gdx-backend-robovm:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
    compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-ios"
    runtime "ch.qos.logback:logback-classic:$logbackClassicVersion"
  }
}

project (":common") {
  apply plugin: "groovy"

  dependencies {
    compile ("com.forerunnergames.tools:fg-tools-common:$fgToolsVersion")
    compile ("com.forerunnergames.tools:fg-tools-net:$fgToolsVersion")
    compile ("com.esotericsoftware:kryonet:2.22.0-RC1") {
      exclude module: "kryo"
    }
    compile "de.javakaffee:kryo-serializers:0.37"
    compile "net.engio:mbassador:1.2.4"
    compile "com.google.guava:guava:19.0"
    compile "com.google.code.findbugs:jsr305:3.0.1"
    compile "org.slf4j:slf4j-api:1.7.13"
  }
}

project (":core") {
  apply plugin: "groovy"

  dependencies {
    compile project (":common")
    compile ("com.stateforge:statemachine:3.6") {
      exclude module: "commons-io"
    }
    testCompile project (path: ":common", configuration: "testRuntime")
  }
}

project (":client") {
  dependencies {
    compile project (":common")
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    compile "com.amazonaws:aws-java-sdk:1.10.44"
    testCompile project (path: ":common", configuration: "testRuntime")
  }
}

project (":server") {
  dependencies {
    compile project (":core")
    compile "com.beust:jcommander:$jcommanderVersion"
    testCompile project (path: ":common", configuration: "testRuntime")
    runtime "ch.qos.logback:logback-classic:$logbackClassicVersion"
  }
}

project (":integration") {
  configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
  }

  dependencies {
    testCompile project (":core")
    testCompile project (":server")
    testCompile project (":client")
    testCompile "org.testng:testng:6.9.10"
  }

  test {
    useTestNG ()
  }
}

// The "test" task doesn"t exist in the android subproject until the android plugin has been applied.
evaluationDependsOn (":android")
// Require subprojects" 'test' tasks to complete before the parent 'test' task.
// This causes all subprojects to run Cobertura before the parent project,
// which allows Cobertura to aggregate subproject coverage results in the parent project.
test.dependsOn (subprojects.test)

// Collect Cobertura data files from all subprojects.
def files = subprojects.collect {
  new File (it.projectDir, "/build/cobertura/cobertura.ser")
}

cobertura {
  coverageFormats = ["html", "xml"]
  coverageMergeDatafiles = files // Aggregate Cobertura data files from subprojects into a single data file.

  // Ensure classes from all subprojects are included in the coverage check & results.
  rootProject.subprojects.each {
    coverageDirs << file ("${it.name}/build/classes/main")
  }
}

idea {
  project {
    languageLevel = "${projectSourceCompatibility}"
  }

  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

tasks.eclipse.doLast {
  delete ".project"
}
