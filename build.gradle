buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:0.14.4"
    classpath "org.robovm:robovm-gradle-plugin:1.0.0-beta-03"
  }
}

allprojects {
  apply plugin: "eclipse"
  apply plugin: "idea"

  group = projectGroup
  version = projectVersion

  ext {
    appName = projectName
    gdxVersion = "1.5.4-SNAPSHOT"
    roboVMVersion = "1.0.0-SNAPSHOT"
    box2DLightsVersion = "1.4-SNAPSHOT"
    ashleyVersion = "1.3.3"
    aiVersion = "1.4.1"
  }

  repositories {
    maven { url "${System.env.HOME}/.m2/repository/" }
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "http://stateforge.com/repository/" }
  }
}

project (":desktop") {
  apply plugin: "java"

  sourceCompatibility = projectSourceCompatibility
  targetCompatibility = projectTargetCompatibility

  dependencies {
    compile project (":client")
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    runtime "ch.qos.logback:logback-classic:1.1.2"
  }
}

project (":android") {
  apply plugin: "android"

  configurations { natives }

  dependencies {
    compile project (":client")
    compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
    compile "org.slf4j:slf4j-android:1.7.9"
  }
}

project (":ios") {
  apply plugin: "java"
  apply plugin: "robovm"

  sourceCompatibility = projectSourceCompatibility
  targetCompatibility = projectTargetCompatibility

  configurations { natives }

  dependencies {
    compile project (":client")
    compile "org.robovm:robovm-rt:${roboVMVersion}"
    compile "org.robovm:robovm-cocoatouch:${roboVMVersion}"
    compile "com.badlogicgames.gdx:gdx-backend-robovm:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
    natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-ios"
    runtime "ch.qos.logback:logback-classic:1.1.2"
  }
}

project (":client") {
  apply plugin: "java"

  sourceCompatibility = projectSourceCompatibility
  targetCompatibility = projectTargetCompatibility

  dependencies {
    compile project (":core")
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    compile "com.esotericsoftware:kryonet:2.22.0-RC1"
    testRuntime "ch.qos.logback:logback-classic:1.1.2"
  }
}

project (":core") {
  apply plugin: "java"
  apply plugin: "groovy"

  sourceCompatibility = projectSourceCompatibility
  targetCompatibility = projectTargetCompatibility

  dependencies {
    compile ("com.forerunnergames.tools:common:1.0-SNAPSHOT")
    compile ("com.forerunnergames.tools:net:1.0-SNAPSHOT")
    compile ("com.stateforge:statemachine:2.4") {
      exclude module: "commons-io"
    }
    compile "net.engio:mbassador:1.2.0"
    compile "com.google.guava:guava:18.0"
    compile "com.google.code.findbugs:jsr305:2.0.3"
    compile "org.slf4j:slf4j-api:1.7.6"
    testCompile "junit:junit:4.11"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.mockito:mockito-core:1.9.5"
    testRuntime "ch.qos.logback:logback-classic:1.1.2"
  }
}

project (":server") {
  apply plugin: "java"

  sourceCompatibility = projectSourceCompatibility
  targetCompatibility = projectTargetCompatibility

  dependencies {
    compile project (":core")
    compile "com.esotericsoftware:kryonet:2.22.0-RC1"
    compile "com.beust:jcommander:1.35"
    runtime "ch.qos.logback:logback-classic:1.1.2"
  }
}

idea {
  project {
    languageLevel = "${projectSourceCompatibility}"
  }

  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

tasks.eclipse.doLast {
  delete ".project"
}
