[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

sourceSets.main.java.srcDirs = ["src/main/java/", "src-gen/main/java"]
sourceSets.main.resources.srcDirs = ["src/main/res/"]
sourceSets.test.java.srcDirs = ["src/test/java/"]
sourceSets.test.resources.srcDirs = ["src/test/res/"]

configurations {
  stateBuilderAntTask
  stateBuilderAntTask.exclude group: "unquietcode.tools.jcodemodel"
}

dependencies {
  stateBuilderAntTask "com.unquietcode.tools.jcodemodel:codemodel:1.0.2"
  stateBuilderAntTask "com.stateforge.statebuilder.ant:statebuilder-ant-task:3.6"
  stateBuilderAntTask "com.stateforge.statebuilder:statebuilder-java:3.6"
}

task generateJava << {
  new File ("${projectDir}/src-gen").deleteDir();
  ant.taskdef (name: "stateBuilder",
          classname: "com.stateforge.statebuilder.ant.StateBuilderAntTask",
          classpath: configurations.stateBuilderAntTask.asPath)
  ant.stateBuilder (input: "${projectDir}/src/main/res", output: "${projectDir}/src-gen/main/java") {}
}

task testJar (type: Jar) {
  classifier = "tests"
  from sourceSets.test.output
  exclude "**/*logback-test.xml"
  exclude "**/*com/forerunnergames/peril/core/model"
}

artifacts {
  testRuntime testJar
}

compileJava.dependsOn (generateJava)
jar.exclude ("*.fsmjava")
test.testLogging.showStandardStreams = true;

eclipse.project {
  name = appName + "-core"
}
